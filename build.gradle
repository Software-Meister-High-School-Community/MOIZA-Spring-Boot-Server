plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

group = 'com.moiza'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()

}

allprojects {
    bootJar {
        enabled = false
    }
}


subprojects {
    group 'com.moiza'
    version '1.0'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'

    sourceCompatibility = 17

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.4'
        testImplementation 'org.assertj:assertj-core:3.22.0'
        testImplementation 'org.mockito:mockito-core:3.8.0'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

springBoot {
    mainClass = 'com.moiza.moizaspringbootserver.MoizaSpringBootServerApplication'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = false
}

sonarqube {
    properties {
        property "sonar.projectKey", "Software-Meister-High-School-Community_MOIZA-Spring-Boot-Server"
        property "sonar.organization", "software-meister-high-school-community"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    dependsOn(subprojects.test)
    dependsOn(subprojects.jacocoTestReport)
    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        xml.enabled true
    }
}
